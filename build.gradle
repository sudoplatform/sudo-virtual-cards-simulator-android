buildscript {
    ext.kotlin_version = '2.1.0'
    ext.aws_version = '2.81.0'
    ext.amplify_version = '2.29.2'
    ext.dokka_version = '2.0.0'

    repositories {
        maven { url = "https://plugins.gradle.org/m2" }
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.12.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.owasp:dependency-check-gradle:12.1.3"
        classpath "org.jmailen.gradle:kotlinter-gradle:5.2.0"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:6.2.0.5505"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "gradle.plugin.com.cookpad.android.plugin:plugin:1.2.8"
        classpath ("com.squareup.moshi:moshi") {
            version{
                strictly '1.8.0'
            }
        }
    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id "com.apollographql.apollo" version "4.3.2"
}

ext {
    projectGroup = 'com.sudoplatform'
}

if(project.file("${rootProject.projectDir}/util/internal-repo.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/internal-repo.gradle"
}
else {
    ext["internalRepoUsername"] = ""
    ext["internalRepoPassword"] = ""
    ext["internalSnapshotRepoUrl"] = ""
    ext["internalSnapshotRepoName"] = ""
    ext["internalReleaseRepoUrl"] = ""
    ext["internalReleaseRepoName"] = ""
    ext["sonatypeStagingProfileId"] = ""
}

if (project.hasProperty("ossIndexUsername")) {
    ext["ossIndexUsername"] = "$ossIndexUsername"
} else {
    ext["ossIndexUsername"] = ""
}

if (project.hasProperty("ossIndexPassword")) {
    ext["ossIndexPassword"] = "$ossIndexPassword"
} else {
    ext["ossIndexPassword"] = ""
}

if (project.findProperty("tag") && sonatypeStagingProfileId) {
    nexusPublishing {
        repositories {
            sonatype {
                nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
                stagingProfileId = sonatypeStagingProfileId
                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven {
            name = "android-releases"
            url = "https://repo.tools.anonyome.com/repository/android-releases/"
            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
        maven {
            name = "android-snapshots"
            url = "https://repo.tools.anonyome.com/repository/android-snapshots/"
            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
        maven { url 'https://jitpack.io' }
    }
    gradle.projectsEvaluated {
        tasks.withType(Test) {
            outputs.upToDateWhen { false } // Force gradle to run unit tests even if everything is up to date
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
