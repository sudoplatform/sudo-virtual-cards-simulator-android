apply plugin: 'com.android.library'
apply plugin: 'com.amazonaws.appsync'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.owasp.dependencycheck'
// Jacoco test coverage
apply plugin: 'jacoco'

// jacoco parser error in versions below 0.8.7:
jacoco {
    toolVersion = "0.8.10"
}

node {
    version = "16.18.0"
}

android {
    compileSdkVersion 31

    namespace "com.sudoplatform.sudovirtualcards.simulator"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 31
        versionCode = 1
        versionName = "8.0.0"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        testApplicationId = "com.sudoplatform.sudovirtualcards.simulator.test"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    resourcePrefix 'svcs_'

    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
        pickFirst("META-INF/kotlinx-coroutines-core.kotlin_module")
        pickFirst("META-INF/sudovirtualcardssimulator_debug.kotlin_module")
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            // Default heap size (512) was starting to cause out of memory issues
            maxHeapSize = '2048m'
            outputs.upToDateWhen {false}
            jacoco {
                includeNoLocationClasses = true
                excludes = [
                        'jdk.internal.*'
                ]
            }
        }
    }


    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    // Kotlin
    def coroutine_version = '1.7.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    // AWS SDKs
    //
    // AWS SDKs do not follow semantic versioning rules so we need to strictly specify AWS SDK versions.
    // Follow the process at https://anonyome.atlassian.net/wiki/spaces/PLA/pages/840073287/AWS+Android+SDK+versions+to+use+for+platform+SDKs
    // for updating AWS SDK versions.
    implementation "com.amazonaws:aws-android-sdk-core:$aws_version!!"
    implementation "com.amazonaws:aws-android-sdk-appsync:$appsync_version!!"
    constraints {
        implementation("com.google.android.gms:play-services-basement:18.1.0") {
            because("Versions < 18.1.0 have CVE-2022-1799")
        }
    }

    // Sudo Platform
    implementation "com.sudoplatform:sudologging:3.0.0"
    implementation "com.sudoplatform:sudoconfigmanager:6.0.0"
    implementation "com.sudoplatform:sudouser:17.0.0"
    implementation "com.sudoplatform:sudovirtualcards:12.0.0"

    implementation "com.appmattus.certificatetransparency:certificatetransparency-android:2.4.7"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"

    // Testing
    def kotlintest_version = "3.4.2"
    testImplementation "junit:junit:4.13.2"
    testImplementation "io.kotlintest:kotlintest-assertions:$kotlintest_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutine_version"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.1.0"
    testImplementation "com.jakewharton.timber:timber:5.0.1"

    // Stripe
    androidTestImplementation "com.stripe:stripe-android:20.5.0"

    // Checkout.com
    androidTestImplementation 'com.github.checkout:frames-android:3.1.2'

    // Checkout dependency only works with 1.3.0 or 1.4.0 so we peg
    // it to 1.4.0 so other test related dependencies do not automatically
    // upgrade it to a later version that breaks the build.
    androidTestImplementation("com.google.android.material:material") {
        version {
            strictly("1.4.0")
        }
    }

    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "io.kotlintest:kotlintest-assertions:$kotlintest_version"
    androidTestImplementation "com.jakewharton.timber:timber:4.7.1"

    androidTestImplementation "com.sudoplatform:sudoprofiles:13.0.1"
    androidTestImplementation "com.sudoplatform:sudokeymanager:6.0.0"
    androidTestImplementation 'com.sudoplatform:sudoentitlements:8.0.1'
    androidTestImplementation 'com.sudoplatform:sudoidentityverification:12.0.1'
}

// Returns a list of `configurations` which should not be scanned for vulnerabilities
def getConfigurationsToSkip = { configs ->
    configs.findAll {
        it.name.startsWithAny("androidTest", "test", "lint") ||
                it.name.contains("AndroidTest") ||
                it.name.contains("UnitTest")
    }.collect {
        it.name
    }
}

tasks.register("printConfigurationsToSkip") {
    group = "OWASP dependency-check"
    description = "Print the configurations which are skipped when scanning for vulnerable dependencies"
    doLast {
        println "Configurations to skip when scanning for vulnerable dependencies:"
        println getConfigurationsToSkip(configurations).join("\n")
    }
}

afterEvaluate {
    // NOTE: this must be within `afterEvaluate` to ensure all the configurations have been created before filtering them
    dependencyCheck {
        suppressionFile file("../dependency-suppression.xml").toString()
        failBuildOnCVSS 0
        skipConfigurations = getConfigurationsToSkip(configurations)

        cve {
            urlModified = "https://anonyome-nist-cve-mirror.s3.amazonaws.com/nvdcve-1.1-modified.json.gz"
            urlBase = "https://anonyome-nist-cve-mirror.s3.amazonaws.com/nvdcve-1.1-%d.json.gz"
            startYear = 2002
            waitTime = 0
        }

        analyzers {
            assemblyEnabled = false
            ossIndex {
                username = "${ossIndexUsername}"
                password = "${ossIndexPassword}"
                warnOnlyOnRemoteErrors = true
            }
        }
    }
}

apply plugin: "com.cookpad.android.plugin.license-tools"

licenseTools {
    licensesYaml = file("licenses.yml")
    ignoredGroups = ["com.nhaarman:mockito-kotlin", "com.anonyome", "com.sudoplatform"]
    ignoredProjects = ["org.mockito"]
}

@Grab('org.yaml:snakeyaml:1.17') // Yaml parser
import org.yaml.snakeyaml.Yaml

task checkLicenseHaveRequiredFields {
    description = "Check every license entry has the mandatory fields"
    group = "verification"

    doLast {
        def mandatoryFields = ["artifact", "name", "copyrightHolder", "url"]
        Yaml parser = new Yaml()
        def licenses = parser.load((file("licenses.yml") as File).text)
        def errors = new StringBuilder()
        licenses.each { artifact ->
            mandatoryFields.each { field ->
                if (artifact[field] == null) {
                    errors.append("License entry '${artifact.artifact}' is missing required element `$field`\n")
                }
            }
            if (artifact["license"] == null && artifact["licenseUrl" == null]) {
                errors.append("License entry '${artifact.artifact}' must have one of [license,licenseUrl]\n")
            }
        }
        if (errors.length() > 0) {
            throw new GradleException("Missing license elements:\n$errors")
        }
    }
}
checkLicenses.finalizedBy checkLicenseHaveRequiredFields

// Kotlin Lint
apply plugin: 'org.jmailen.kotlinter'

kotlinter {
    ignoreFailures = false
    reporters = ['checkstyle', 'plain']
    experimentalRules = false
}

// Apollo Code Generation
apollo {
    outputPackageName = "com.sudoplatform.sudovirtualcards.simulator.graphql"
}

// Jacoco test coverage
apply plugin: 'jacoco'

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"
    classDirectories.setFrom(fileTree(
            dir: "${buildDir}/tmp/kotlin-classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
    )
    )
    sourceDirectories.setFrom(files("${buildDir.parent}/sudovirtualcardssimulator/src/main/java"))
    executionData.setFrom(fileTree(["dir":"$buildDir", "includes":["**/*.exec", "**/*.ec"]]))
}

// Dokka documentation generation
apply plugin: 'org.jetbrains.dokka'

dokkaHtml {
    outputDirectory.set(project.file("$buildDir/javadoc"))

    dokkaSourceSets {
        configureEach {
            jdkVersion.set(8)
            skipEmptyPackages.set(true)
            includes.from(files("packages.md"))
            samples.from(files("src/test/java/com/sudoplatform/sudovirtualcards/simulator/samples/Samples.kt"))
        }
    }
}

// Sonarqube code analysis
if (project.file("${rootProject.projectDir}/util/sonarqube.gradle").exists()) {
    ext.sonarProjectKey = "sudo-virtual-cards-android-simulator"
    ext.sonarProjectName = "sudo-virtual-cards-android-simulator"
    ext.sonarExclusions = ["*.png", "*.jks", "*.json", "*.key"]
    apply from: "${rootProject.projectDir}/util/sonarqube.gradle"
}

// Setup common publishing variables
ext {
    projectArtifact = project.name
    projectDescription = 'Sudo Virtual Cards Simulator SDK for the Sudo Platform by Anonyome Labs.'
    projectUrl = 'https://github.com/sudoplatform/sudo-virtual-cards-simulator-android'
    projectSCM = 'scm:git:github.com/sudoplatform/sudo-virtual-cards-simulator-android.git'
    if (project.findProperty("tag")) {
        projectVersion = "$tag"
    } else {
        projectVersion = project.android.defaultConfig.versionName
    }
}

// Internal and External publishing
if(project.findProperty("tag") &&
        project.file("${rootProject.projectDir}/util/publish-mavencentral.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/publish-mavencentral.gradle"
}
else if (project.file("${rootProject.projectDir}/util/publish-internal-android.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/publish-internal-android.gradle"
}
